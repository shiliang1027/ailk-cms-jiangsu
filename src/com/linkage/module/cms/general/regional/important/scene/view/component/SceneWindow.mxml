<?xml version="1.0" encoding="utf-8"?>
<panel:Window xmlns:fx="http://ns.adobe.com/mxml/2009"
			  xmlns:s="library://ns.adobe.com/flex/spark"
			  xmlns:mx="library://ns.adobe.com/flex/mx"
			  xmlns:component="com.linkage.module.cms.general.regional.important.scene.view.component.*"
			  xmlns:panel="com.linkage.system.component.panel.*"
			  mouseEnabled="true"
			  width="100%"
			  height="100%"
			  closable="false"
			  maximizable="false"
			  minimizable="false">
	<fx:Script>
		<![CDATA[
			import com.linkage.module.cms.general.regional.important.scene.common.event.SceneEvent;
			import com.linkage.module.cms.general.regional.important.scene.common.util.FileUtil;
			import com.linkage.module.cms.general.regional.important.scene.controller.MonitorAction;

			import mx.core.UIComponent;
			import mx.logging.ILogger;
			import mx.logging.Log;

			/**
			 * 日志记录器
			 */
			private var log:ILogger=Log.getLogger("com.linkage.module.cms.general.regional.important.scene.view.component.SceneWindow");
			/**
			 * 场馆监控控制器
			 */
			private var monitorAction:MonitorAction=null;
			/**
			 * 场馆背景图片容器
			 */
			private var skinUi:UIComponent=null;

			//初始化窗口信息
			public function initWindow(monitAction:MonitorAction):void
			{
				//1.初始化参数
				monitorAction=monitAction;
				sceneList.monitorAction=monitAction;

				//2.加载场馆列表
				loadSceneList();

				//3.加载场景数据信息
				sceneInfo.monitorAction=monitAction;

				//4.初始化场馆基本信息
				baseInfo.monitorAction=monitAction;
				baseInfo.sceneWindow=this;
				ensurePlan.monitorAction=monitAction;
				sportsInfo.monitorAction=monitAction;

				//6.场馆发生变更
				monitorAction.addEventListener(SceneEvent.SCENE_CLICK, scenChange);
			}

			//场馆发生变更
			private function scenChange(event:SceneEvent):void
			{
				var venueId:String=event.venueId;
				var venueURL:String=event.venueURL;
				var histroyTime:String=event.histroyTime;

				log.info("场景变化处理，venueId：" + venueId + " venueURL：" + venueURL + " histroyTime：" + histroyTime);

				//1.地图背景变化
				initSceneBackGround(venueId, venueURL);

				//2.初始化场景数据
				sceneInfo.initInfo(venueId, histroyTime);

				//3.初始化场馆基本信息
				baseInfo.loadBaseInfo(venueId);
				sportsInfo.loadSportsInfo(venueId);
				ensurePlan.loadEnsurePlanInfo(venueId);

				//4.初始化网元信息
				loadVenueNeInfo(venueId, histroyTime);
			}

			//初始化网元信息
			private function loadVenueNeInfo(sceneId:String, histroyTime:String):void
			{
				neInfo.removeAllElements();
				//1.网元信息
				monitorAction.getAllVenueNe(sceneId, function(result:Object):void
					{
						initVenueNeInfo(sceneId, histroyTime, result);
					});
			}

			//初始化网元信息
			private function initVenueNeInfo(sceneId:String, histroyTime:String, result:Object):void
			{
				var venueNe:VenueNeInfo=null;
				for each (var obj:Object in result)
				{
					venueNe=new VenueNeInfo();
					venueNe.monitorAction=monitorAction;
					venueNe.initNeInfo(sceneId, histroyTime, obj, this);
					neInfo.addElement(venueNe);
				}
			}

			//初始化指标信息
			public function initNeSegment(neSegmentSet:NeSegmentSet):void
			{
				neInfo.addElement(neSegmentSet);
			}

			//加载场馆列表
			private function loadSceneList():void
			{
				monitorAction.getVenueListInfo(function(result:Object):void
					{
						sceneList.initList(result);
					});
			}

			//初始化场馆背景图片
			private function initSceneBackGround(venueId:String, venueURL:String):void
			{
				log.info("venueId=" + venueId + ",venueURL=" + venueURL)
				FileUtil.urlLoader(venueURL, function(result:Object):void
					{
						var bitmap:Bitmap=result.content as Bitmap;
						addImageInfo(venueId, bitmap);

					}, function(event:IOErrorEvent):void
					{
						log.info("加载场景图片失败.");
					});
			}

			//添加图片信息
			private function addImageInfo(venueId:String, bitmap:Bitmap):void
			{
				if (bitmap != null)
				{
					bitmap.width=width;
					bitmap.height=height;
					if (skinUi == null)
					{
						skinUi=new UIComponent();
						skinUi.width=width;
						skinUi.height=height;
						skinUi.addEventListener(MouseEvent.CLICK, skinUiClickHandler);
						sceneMonitor.addElementAt(skinUi, 0);
					}
					if (skinUi.numChildren > 0)
					{
						skinUi.removeChildAt(0);
					}
					skinUi.addChild(bitmap);
				}
			}

			//显示场馆基本信息
			protected function skinUiClickHandler(event:MouseEvent):void
			{
				baseInfo.windowOpenClose();
			}

			//展示当前场景监控
			public function showSceneMonitor(venueId:String, historyTime:String=null):void
			{
				sceneList.showSceneMonitor(venueId, historyTime);
			}

			//展示告警
			public function showAlarm():void
			{
				sceneInfo.showAlarm();
			}

			//隐藏告警
			public function hideAlarm():void
			{
				sceneInfo.hideAlarm();
			}
		]]>
	</fx:Script>
	<s:Scroller top="-20"
				width="100%"
				height="100%"
				verticalScrollPolicy="auto"
				horizontalScrollPolicy="auto">
		<s:Group id="sceneMonitor"
				 width="100%"
				 height="100%">
			<s:Group id="neInfo"
					 width="100%"
					 height="100%"/>
			<component:SportsInfo id="sportsInfo"
								  left="39"
								  width="300"
								  height="200"
								  visible="false"
								  includeInLayout="false"/>
			<component:EnsurePlan id="ensurePlan"
								  left="39"
								  width="300"
								  height="160"
								  visible="false"
								  includeInLayout="false"/>
			<component:SceneBaseInfo id="baseInfo"
									 top="210"
									 left="260"
									 width="276"
									 height="230"/>
			<component:SceneInfo id="sceneInfo"
								 width="396"
								 height="236"/>
			<component:SceneList id="sceneList"/>
		</s:Group>
	</s:Scroller>
</panel:Window>
