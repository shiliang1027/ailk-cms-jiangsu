<?xml version="1.0" encoding="utf-8"?>
<datashow:DataWindow xmlns:fx="http://ns.adobe.com/mxml/2009"
					 xmlns:s="library://ns.adobe.com/flex/spark"
					 xmlns:mx="library://ns.adobe.com/flex/mx"
					 xmlns:datashow="com.linkage.module.cms.general.monitor.datashow.*"
					 width="100%"
					 height="100%">
	<datashow:layout>
		<s:BasicLayout/>
	</datashow:layout>
	<fx:Script>
		<![CDATA[
			import com.linkage.module.cms.components.loadmanager.LoadManager;
			import com.linkage.module.cms.general.monitor.datamanager.RemoteDataManager;
			import com.linkage.system.logging.ILogger;
			import com.linkage.system.logging.Log;
			import com.linkage.system.utils.StringUtils;

			import mx.collections.ArrayCollection;
			import mx.collections.ArrayList;
			import mx.containers.Canvas;
			import mx.controls.Alert;
			import mx.controls.advancedDataGridClasses.AdvancedDataGridColumn;
			import mx.events.ListEvent;

			import spark.components.Group;

			//日志
			public var log:ILogger = Log.getLogger("com.linkage.module.cms.general.monitor.datashow.workState");

			//数据接口
			private var _remoteData:RemoteDataManager = null;

			//属地编号
			private var _cityId:String = null;

			//专业列表
			private var _specList:ArrayList = null;

			//定时调度
			private var t:Timer = new Timer(300000);

			public var workStateList:ArrayCollection = new ArrayCollection([
				{title: "工程统计"
					, name: ["区域", "总数", "核心网", "2G无线网", "TD无线网"]
					, column: ["city", "total", "core", "wire_2g", "wire_td"]
					, datalist: [{city: "全省", total: "10", core: "2", wire_2g: "2", wire_td: "2"}
						, {city: "南京", total: "8", core: "2", wire_2g: "2", wire_td: "2"}
						, {city: "苏州", total: "2", core: "2", wire_2g: "2", wire_td: "2"}]}
				]);

			public function get specList():ArrayList
			{
				return _specList;
			}

			public function set specList(value:ArrayList):void
			{
				_specList = value;
			}

			public function get cityId():String
			{
				return _cityId;
			}

			public function set cityId(value:String):void
			{
				_cityId = value;
			}

			public function get remoteData():RemoteDataManager
			{
				return _remoteData;
			}

			public function set remoteData(value:RemoteDataManager):void
			{
				_remoteData = value;
			}

			override public function initData():void
			{
				//显示等待界面
				//loadInfo = new LoadManager(this.info);
				//loadInfo.showLoading("正在加载数据，请稍候");

				//加载数据
				loadData();

				//定时调度
				t.addEventListener(TimerEvent.TIMER, loadData);
				t.start();
				//-------------------------------------历史回退修改------------------------------------
				this.parentApplication.add(t);
				//-------------------------------------历史回退修改------------------------------------
			}

			//显示页面，启动定时任务
			override public function showView():void
			{
				if(t != null && !t.running)
				{
					t.start();
					//-------------------------------------历史回退修改------------------------------------
					this.parentApplication.add(t);
						//-------------------------------------历史回退修改------------------------------------
				}
			}

			//隐藏页面，停止定时任务
			override public function hideView():void
			{
				if(t != null && t.running)
				{
					t.stop();
					//-------------------------------------历史回退修改------------------------------------
					this.parentApplication.remove(t);
						//-------------------------------------历史回退修改------------------------------------
				}
			}

			//加载数据
			public function loadData(event:TimerEvent = null):void
			{
				if(_cityId != null && !StringUtils.isEmpty(_cityId))
				{
					_remoteData.getCityProjectData(_cityId, _specList, getResInfo, null, getResInfoErr);
				}
				else
				{
					_remoteData.getAllProvinceProjectData(_specList, getResInfo, null, getResInfoErr);
				}
			}

			public function getResInfo(obj:Object):void
			{
				log.info("工程状态统计结果：" + obj);
				this.grid.visible = true;
				if(obj != null && obj[0] != null)
				{
					createTable(obj);
					obj = null;
				}
				//查询无数据
				else
				{
					log.info("工程状态统计：查询无数据");
					Alert.show("查询工程状态统计无数据！");
						//createTable(workStateList);
				}

				//loadInfo.hideLoading();
			}

			private function createTable(obj:Object):void
			{
				//生成列
				if(this.grid.columns == null || this.grid.columns.length != obj[0].column.length)
				{
					var colArr:Array = new Array();
					for(var i:String in obj[0].column)
					{
						var col:AdvancedDataGridColumn = new AdvancedDataGridColumn();
						col.dataField = obj[0].column[i];
						col.headerText = obj[0].name[i];
						col.width = 100;
						colArr = colArr.concat(col);
					}
					this.grid.columns = colArr;
				}

				//数据
				this.grid.dataProvider = obj[0].datalist;

				obj = null;
			}

			public function getResInfoErr(obj:Object):void
			{
				log.info("工程状态统计：查询数据异常");
				Alert.show("查询工程状态统计异常！");
				//createTable(workStateList);
				//loadInfo.hideLoading();
			}

			override public function hideDataGroup():void
			{
				this.showGroup.visible = false;
				this.showGroup.includeInLayout = false;
			}

			override public function showDataGroup():void
			{
				this.showGroup.visible = true;
				this.showGroup.includeInLayout = true;
			}

			override public function showGroupInfo():Group
			{
				return this.showGroup;
			}

			protected function grid_itemClickHandler(e:ListEvent):void
			{
				var cityName:String = e.target.dataProvider[e.rowIndex].city;
				log.info("过滤工程：cityName：" + cityName + '_cityId:' + _cityId + ' rowIndex:' + e.rowIndex + ' columnIndex:' + e.columnIndex);

				//取当前点击的属地和网元类型
				if(e.rowIndex > 0 && e.columnIndex == 0)
				{
					if(this._cityId == "")
					{
						general.locationView(cityName, "2");
						general.showFlowInfo(3);
					}
					else
					{
						general.locationView(cityName, "3");
					}
				}

				if(e.columnIndex > 0)
				{
					if(this._cityId == "")
					{
						general.reloadFlowData(3, cityName, "2");
					}
					else
					{
						if(e.rowIndex == 0)
							general.reloadFlowData(3, cityName, "2");
						else
							general.reloadFlowData(3, cityName, "3");

					}
				}
			}
		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
	</fx:Declarations>
	<s:VGroup id="showGroup"
			  width="100%"
			  height="100%"
			  visible="false"
			  includeInLayout="false">
		<s:Scroller width="100%"
					height="100%"
					horizontalScrollPolicy="auto"
					verticalScrollPolicy="auto">
			<s:Group>
				<mx:AdvancedDataGrid sortableColumns="false"
									 visible="false"
									 height="100%"
									 id="grid"
									 horizontalScrollPolicy="off"
									 verticalScrollPolicy="auto"
									 itemClick="grid_itemClickHandler(event)">
				</mx:AdvancedDataGrid>
			</s:Group>
		</s:Scroller>
	</s:VGroup>
</datashow:DataWindow>
